// how much elevation is affected by Y-coordinate
let slope =
    0.50
in

// how much temperature is affected by elevation
let geotemp =
    0.25
in


// elevation, temperature and humidity axes
let elevation =
    let y =
        Y{}
    in
    let raw_elevation =
        Perlin{ octaves: 6.0; frequency: 1.0; persistence: 0.45 }
    in
    (1.0 - slope)*raw_elevation + slope*y
in
let temperature =
    let raw_temperature =
        Perlin{ octaves: 6.0; frequency: 0.5; persistence: 0.3 }
    in
    (1.0 - geotemp)*raw_temperature - geotemp*elevation
in
let humidity =
    Perlin{ octaves: 6.0; frequency: 0.5; persistence: 0.3 }
in


// elevation, temperature and humidity zones
let high = 1.0 / 4.0 in
let low  = -1.0 / 4.0 in

let lo_ele_zone =
    elevation < low
in
let hi_ele_zone =
    elevation > high
in
let md_ele_zone =
    not hi_ele_zone and not lo_ele_zone
in

let lo_hum_zone =
    humidity < low
in
let hi_hum_zone =
    humidity > high
in
let md_hum_zone =
    not hi_hum_zone and not lo_hum_zone
in

let lo_temp_zone =
    temperature < low
in
let hi_temp_zone =
    temperature > high
in
let md_temp_zone =
    not hi_temp_zone and not lo_temp_zone
in


// biomes
if hi_ele_zone and hi_temp_zone and hi_hum_zone then #490071 else
if hi_ele_zone and hi_temp_zone and md_hum_zone then #680024 else
if hi_ele_zone and hi_temp_zone and lo_hum_zone then #6e0000 else

if hi_ele_zone and md_temp_zone and hi_hum_zone then #002471 else
if hi_ele_zone and md_temp_zone and md_hum_zone then #2f1d22 else
if hi_ele_zone and md_temp_zone and lo_hum_zone then #3e1a00 else

if hi_ele_zone and lo_temp_zone and hi_hum_zone then #003670 else
if hi_ele_zone and lo_temp_zone and md_hum_zone then #002e20 else
if hi_ele_zone and lo_temp_zone and lo_hum_zone then #002c00 else


if md_ele_zone and hi_temp_zone and hi_hum_zone then #ba4cde else
if md_ele_zone and hi_temp_zone and md_hum_zone then #df4281 else
if md_ele_zone and hi_temp_zone and lo_hum_zone then #eb3d20 else

if md_ele_zone and md_temp_zone and hi_hum_zone then #347fdd else
if md_ele_zone and md_temp_zone and md_hum_zone then #90797f else
if md_ele_zone and md_temp_zone and lo_hum_zone then #a47615 else

if md_ele_zone and lo_temp_zone and hi_hum_zone then #0097dc else
if md_ele_zone and lo_temp_zone and md_hum_zone then #00927d else
if md_ele_zone and lo_temp_zone and lo_hum_zone then #429005 else


if lo_ele_zone and hi_temp_zone and hi_hum_zone then #ffbdff else
if lo_ele_zone and hi_temp_zone and md_hum_zone then #ffb6ed else
if lo_ele_zone and hi_temp_zone and lo_hum_zone then #ffb284 else

if lo_ele_zone and md_temp_zone and hi_hum_zone then #b7ebff else
if lo_ele_zone and md_temp_zone and md_hum_zone then #ffe5eb else
if lo_ele_zone and md_temp_zone and lo_hum_zone then #ffe180 else

if lo_ele_zone and lo_temp_zone and hi_hum_zone then #a3ffff else
if lo_ele_zone and lo_temp_zone and md_hum_zone then #8dffe9 else
                                                     #b4ff7c
